MACRO(FRENSIE_PROCESS_ROOT_GEOM FILE_NAME_BASE)
  SET(options VERBOSE)
  SET(oneValueArgs SET_VERBOSE FILE_NAME_SUFFIX)
  SET(multiValueArgs)
  CMAKE_PARSE_ARGUMENTS(FRENSIE_PROCESS_ROOT_GEOM "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  # Check for unused parameters
  IF(NOT "${FRENSIE_PROCESS_ROOT_GEOM_UNPARSED_ARGUMENTS}" STREQUAL "")
    MESSAGE(WARNING "${PACKAGE_NAME} root geom processing unused parameters = ${FRENSIE_PROCESS_ROOT_GEOM_UNPARSED_ARGUMENTS}")
  ENDIF()

  # Determine the verbosity that will be used
  SET(VERBOSE_PROCESSING_REQUIRED ${FRENSIE_PROCESS_ROOT_GEOM_VERBOSE})

  IF(NOT "${FRENSIE_PROCESS_ROOT_GEOM_SET_VERBOSE}" STREQUAL "")
    IF(${FRENSIE_PROCESS_ROOT_GEOM_SET_VERBOSE})
      SET(VERBOSE_PROCESSING_REQUIRED ${FRENSIE_SETUP_PACKAGE_SET_VERBOSE})
    ENDIF()
  ENDIF()

  # Construct the file name
  IF(NOT ${FRENSIE_PROCESS_ROOT_GEOM_FILE_NAME_SUFFIX} STREQUAL "")
    SET(LOCAL_FILE_NAME "${FILE_NAME_BASE}.${FRENSIE_PROCESS_ROOT_GEOM_FILE_NAME_SUFFIX}")
  ELSE()
    SET(LOCAL_FILE_NAME "${FILE_NAME_BASE}.c")
  ENDIF()

  FIND_FILE(FILE_NAME ${LOCAL_FILE_NAME}
    HINTS ${CMAKE_CURRENT_SOURCE_DIR})
  
  STRING(REGEX REPLACE "\/" "_" PROCESSED_FILE_NAME ${FILE_NAME})
  STRING(REGEX REPLACE "\\." "_" PROCESSED_FILE_NAME ${PROCESSED_FILE_NAME})
  
  # This file needs to be processed
  IF((NOT DEFINED FRENSIE_PROCESS_ROOT_GEOM_${PROCESSED_FILE_NAME}_PROCESSED) OR (NOT ${FRENSIE_PROCESS_ROOT_GEOM_${PROCESSED_FILE_NAME}_PROCESSED}))
    
    # Display details if verbose processing has been requested
    IF(${VERBOSE_PROCESSING_REQUIRED})
      MESSAGE(STATUS "root geometry ${FILE_NAME} processing details:\n")
    ENDIF()

    EXECUTE_PROCESS(COMMAND ${ROOT_EXE} ${FILE_NAME}
      WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
      RESULT_VARIABLE PROCESSING_RESULT
      OUTPUT_VARIABLE PROCESSING_OUTPUT
      ERROR_VARIABLE PROCESSING_ERROR)
    
    IF(${VERBOSE_PROCESSING_REQUIRED})
      IF(NOT "${PROCESSING_ERROR}" STREQUAL "")
        MESSAGE(${PROCESSING_ERROR})
      ENDIF()
      
      IF(NOT "${PROCESSING_OUTPUT}" STREQUAL "")
        MESSAGE(${PROCESSING_OUTPUT})
      ENDIF()
    ENDIF()

    SET(FRENSIE_PROCESS_ROOT_GEOM_${PROCESSED_FILE_NAME}_PROCESSED "ON"
      CACHE BOOL "Flag that indicates if the root geometry file has been processed." FORCE)

    MESSAGE(STATUS "root geometry \"${FILE_NAME}\" processed")

    UNSET(VERBOSE_PROCESSING_REQUIRED)
    UNSET(LOCAL_FILE_NAME)
    UNSET(PROCESSED_FILE_NAME)
  ENDIF()

  UNSET(FILE_NAME CACHE)
    
ENDMACRO(FRENSIE_PROCESS_ROOT_GEOM)
